plugins {
    id "java"
    id "eclipse"
    id "checkstyle"
    id "com.github.spotbugs" version "4.7.9"
    id "com.diffplug.spotless" version "6.2.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    google()
}

ext {
    gdxVersion = "1.10.0"
    aiVersion = "1.8.2"
    gsonVersion = "2.7"
}

dependencies {
    implementation "io.github.pm-dungeon:core:1.0.+"

    // LibGDX
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"

    // https://mvnrepository.com/artifact/org.tensorflow/tensorflow
    implementation 'org.tensorflow:tensorflow:1.15.0'

    // JUnit 4 for testing:
    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:4.13.2"

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: "com.google.code.gson", name: "gson", version: gsonVersion
}

sourceCompatibility = 17
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["assets/"]
sourceSets.test.java.srcDirs = ["test/"]

project.ext.assetsDir = new File("assets")

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

checkstyle {
    configFile file("checkstyle-config/checks.xml")
    toolVersion "9.2.1"
    showViolations = true
}

spotbugs {
    toolVersion = "4.2.2"
    ignoreFailures = true
    showStackTraces = false
}

spotbugsMain {
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugsXml.xml")
        }
    }
}

spotless {
    java {
        googleJavaFormat().aosp()
    }
}